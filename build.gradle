buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
    }
}

version "1.0.0"
group "io.github.Dem07"

apply plugin:"org.grails.grails-plugin"
apply plugin:"maven-publish"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation "org.grails:grails-core"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-interceptors"
    profile "org.grails.profiles:web-plugin"
    testImplementation "org.grails:grails-web-testing-support"
}

groovydoc {
    includePrivate = false
    docTitle = "Grails Maintenance Mode Plugin ${version}"
    windowTitle = "Grails Maintenance Mode Plugin ${version}"
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}
// enable if you wish to package this plugin as a standalone application
bootJar.enabled = false

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Grails Maintenance Mode Plugin'
                description = 'A simple, flexible plugin for implementing maintenance mode in Grails applications'
                url = 'https://github.com/Dem07/grails-web-maintenance-mode/tree/master'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'Dem07'
                        name = 'Dariusz BÄ…kowski'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/Dem07/grails-maintenance-mode.git'
                    developerConnection = 'scm:git:ssh://github.com:Dem07/grails-maintenance-mode.git'
                    url = 'https://github.com/Dem07/grails-maintenance-mode'
                }
            }
        }
    }
}